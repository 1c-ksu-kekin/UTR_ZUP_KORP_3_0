
&НаСервере
Процедура ЮТР_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВыполнениеПослеОбработчиковСобытия("ЮТР_ПриСозданииНаСервереПосле", Неопределено);
	
КонецПроцедуры

&НаСервере 
Процедура ЮТР_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка, ДополнительныйПараметр)
	
	//ЮТР начало изменения 
	//{33 432}, 01.06.2015
	//Видимость реквизита ЮТР_ВидДокумента в зависимости от типа объекта потребителя
	Если Объект.ТипОбъектаПотребителя = "Документ.КадровыйПеревод" Тогда
		Элементы.ЮТР_ВидДокумента.Видимость = Истина;
	КонецЕсли;

	//ЮТР окончание изменения 

КонецПроцедуры

&НаСервере
Процедура ЮТР_ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(ПредставлениеОбъектаПотребителя) Тогда 
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
    	Сообщение.Текст = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Объект конфигурации'"));
    	Сообщение.Поле = "ПредставлениеОбъектаПотребителя";
    	Сообщение.Сообщить();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПредставлениеОбъектаДокументооборота) Тогда 
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
    	Сообщение.Текст = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Объект 1С:Документооборота'"));
    	Сообщение.Поле = "ПредставлениеОбъектаДокументооборота";
    	Сообщение.Сообщить();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ТипОбъектаПотребителя) Тогда 
		Запрос = Новый Запрос;
		
		//ЮТР начало изменения 
		//{33 432}, 01.06.2015
		//Добавлен выбор типа объекта потребителя по виду документа для Кадрового перевода
		
		//Запрос.Текст = 
		
		//"ВЫБРАТЬ
		//|	ПравилаИнтеграцииС1СДокументооборотом.Ссылка
		//|ИЗ
		//|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК ПравилаИнтеграцииС1СДокументооборотом
		//|ГДЕ
		//|	ПравилаИнтеграцииС1СДокументооборотом.ТипОбъектаПотребителя = &ТипОбъектаПотребителя
		//|	И НЕ ПравилаИнтеграцииС1СДокументооборотом.ПометкаУдаления
		//|	И ПравилаИнтеграцииС1СДокументооборотом.Ссылка <> &Ссылка
		//|	И ПравилаИнтеграцииС1СДокументооборотом.ЮТР_ВидДокумента = &ЮТР_ВидДокумента";
		//
		//Запрос.УстановитьПараметр("ТипОбъектаПотребителя", Объект.ТипОбъектаПотребителя);
		//Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);

		Если Объект.ТипОбъектаПотребителя = "Документ.КадровыйПеревод" Тогда
			ЗаполненВидДокумента = Истина;
			Запрос.УстановитьПараметр("ЮТР_ВидДокумента", Объект.ЮТР_ВидДокумента);
		Иначе
			ЗаполненВидДокумента = Ложь;
		КонецЕсли;
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПравилаИнтеграцииС1СДокументооборотом.Ссылка
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК ПравилаИнтеграцииС1СДокументооборотом
		|ГДЕ
		|	ПравилаИнтеграцииС1СДокументооборотом.ТипОбъектаПотребителя = &ТипОбъектаПотребителя"
		+ ?(ЗаполненВидДокумента, " И ПравилаИнтеграцииС1СДокументооборотом.ЮТР_ВидДокумента = &ЮТР_ВидДокумента", "") + "
		|	И НЕ ПравилаИнтеграцииС1СДокументооборотом.ПометкаУдаления
		|	И ПравилаИнтеграцииС1СДокументооборотом.Ссылка <> &Ссылка";
		
		Запрос.УстановитьПараметр("ТипОбъектаПотребителя", Объект.ТипОбъектаПотребителя);
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		
		//ЮТР окончание изменения
		
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда 
			Отказ = Истина;
		
			Сообщение = Новый СообщениеПользователю();
    		Сообщение.Текст = НСтр("ru = 'Для данного объекта конфигурации уже введено правило заполнения реквизитов'");
    		Сообщение.Поле = "ПредставлениеОбъектаПотребителя";
    		Сообщение.Сообщить();
		КонецЕсли;
		
		// Проверка заполнения реквизитов табличной части
		Для каждого Строка из ПравилаЗаполненияРеквизитовПриВыгрузке Цикл
			Если Строка.ОбязательноеЗаполнение 
				И НЕ (ЗначениеЗаполнено(Строка.ИмяРеквизитаОбъектаПотребителя) 
				ИЛИ ЗначениеЗаполнено(Строка.ЗначениеРеквизитаДокументооборота) 
				ИЛИ ЗначениеЗаполнено(Строка.ВычисляемоеВыражение)) Тогда
				Отказ = Истина;
				
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = НСтр("ru = 'Не установлено обязательное правило заполнения для реквизита """+Строка.ПредставлениеРеквизитаОбъектаДокументооборота+"""'");
				Сообщение.Поле = "ПравилаЗаполненияРеквизитовПриВыгрузке["+Строка.ПолучитьИдентификатор()+"].ИмяРеквизитаОбъектаПотребителя";
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьВыполнениеОбработчиковСобытия(Ложь);	

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ТипОбъектаПотребителяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УстановитьВыполнениеПослеОбработчиковСобытия("ЮТР_ТипОбъектаПотребителяОбработкаВыбораПосле", Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ТипОбъектаПотребителяОбработкаВыбораПосле(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, ДополнительныйПараметр)
	
	УстановитьВидимостьРеквизитаВидДокумента(ВыбранноеЗначение);

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ТипОбъектаПотребителяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ЮТР_ТипОбъектаПотребителяНачалоВыбораЗавершение", ЭтаФорма);
	ПараметрФормы = Новый Структура;
	ПараметрФормы.Вставить("ТекущаяСтрока", Объект.ТипОбъектаПотребителя);
	
	Результат = ОткрытьФорму("Справочник.ПравилаИнтеграцииС1СДокументооборотом.Форма.ВыборОбъектаМетаданных", 
		ПараметрФормы, ЭтаФорма ,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	УстановитьВыполнениеОбработчиковСобытия(Ложь);

КонецПроцедуры

&НаКлиенте 
Процедура ЮТР_ТипОбъектаПотребителяНачалоВыбораЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда 
		Объект.ТипОбъектаПотребителя = Результат.Имя;
		ПредставлениеОбъектаПотребителя = Результат.Синоним;
		Модифицированность = Истина;
		ЗаполнитьПоУмолчаниюПриЗагрузкеНаСервере(Истина);
		
		УстановитьВидимостьРеквизитаВидДокумента(Результат.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьРеквизитаВидДокумента(пЗначение)
	
	Если пЗначение = "Документ.КадровыйПеревод" Тогда
		Элементы.ЮТР_ВидДокумента.Видимость = Истина;
	Иначе
		Элементы.ЮТР_ВидДокумента.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры