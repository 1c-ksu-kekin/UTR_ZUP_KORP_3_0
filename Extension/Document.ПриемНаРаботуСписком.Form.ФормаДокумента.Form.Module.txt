
&НаКлиенте
Процедура ЮТР_ПослеЗаписи(ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(Объект.Сотрудники[0].ГрафикРаботы) И Объект.Проведен Тогда
		ЮТР_ОповещениеПользователейКлиент.ОповеститьРуководителяСтруктурногоПодразделения(Объект.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_СотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	НоваяСтрока = Объект.Сотрудники.Добавить();
	//ЮТР начало изменения 
	//{32 778}, 30.04.2015
	//Использование реквизитов ЮТР_Аванс, ЮТР_СпособРасчетаАванса справочника Организации		
	ПолучитьАвансПоЗначниюОрганизации(Объект.Организация, НоваяСтрока.Аванс, НоваяСтрока.СпособРасчетаАванса);
	//ЮТР окончание изменения
	
	Элементы.Сотрудники.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	
	РедактироватьСтроку();
	УстановитьВыполнениеОбработчиковСобытия(Ложь);

КонецПроцедуры

//ЮТР начало изменения
//{32 778}, 30.04.2015
//Использование реквизитов ЮТР_Аванс, ЮТР_СпособРасчетаАванса справочника Организации		
&НаСервере
Процедура ПолучитьАвансПоЗначниюОрганизации(пОрганизация, пАванс, пСпособРасчетаАванса)
	ЮТР_РасчетЗарплатыФормы.ПолучитьАвансПоЗначениюОрганизации(пОрганизация, пАванс, пСпособРасчетаАванса);
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЮТР_СотрудникиОбработкаВыбораНаСервере(ВыбранноеЗначение);
	УстановитьВыполнениеОбработчиковСобытия(Ложь);
КонецПроцедуры

&НаСервере
Процедура ЮТР_СотрудникиОбработкаВыбораНаСервере(ВыбранныеСотрудники)

	Если ТипЗнч(ВыбранныеСотрудники) = Тип("Массив") Тогда
		СписокСотрудников = ВыбранныеСотрудники;
	Иначе
		СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбранныеСотрудники);
	КонецЕсли;
	
	СтрокаШаблон = Неопределено;
	Если Объект.Сотрудники.Количество() > 0 Тогда
		
		ПоследняяСтрокаСотрудников = Объект.Сотрудники[Объект.Сотрудники.Количество() - 1];
		Если ЗначениеЗаполнено(ПоследняяСтрокаСотрудников.ДатаПриема)
			И (ЗначениеЗаполнено(ПоследняяСтрокаСотрудников.Должность)
				ИЛИ ЗначениеЗаполнено(ПоследняяСтрокаСотрудников.ДолжностьПоШтатномуРасписанию)) Тогда
			СтрокаШаблон = ПоследняяСтрокаСотрудников;
		КонецЕсли;
		
	КонецЕсли; 
	
	ОписаниеДанныхПоследнейСтрокиСотрудника = Неопределено;
	Для каждого Сотрудник Из СписокСотрудников Цикл
		
		Если Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник)).Количество() > 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		НоваяСтрокаСотрудников = Объект.Сотрудники.Добавить();
		//ЮТР начало изменения 
		//{32 778}, 30.04.2015
		//Использование реквизитов ЮТР_Аванс, ЮТР_СпособРасчетаАванса справочника Организации		
		ПолучитьАвансПоЗначниюОрганизации(Объект.Организация, НоваяСтрокаСотрудников.Аванс, НоваяСтрокаСотрудников.СпособРасчетаАванса);
		//ЮТР окончание изменения
		
		Если ОписаниеДанныхПоследнейСтрокиСотрудника = Неопределено
			И ПоследняяСтрокаСотрудников <> Неопределено Тогда
			
			ОписаниеДанныхПоследнейСтрокиСотрудника = ЗарплатаКадрыРасширенныйВызовСервера.СтруктураПоМетаданным("Документ.ПриемНаРаботу");
			ЗарплатаКадрыРасширенныйКлиентСервер.ЗаполнитьОбъектПоОбразцу(ОписаниеДанныхПоследнейСтрокиСотрудника, Объект, ПоследняяСтрокаСотрудников, "Сотрудник");
			
		КонецЕсли; 
		
		Если ОписаниеДанныхПоследнейСтрокиСотрудника <> Неопределено Тогда
			
			ОписаниеДанныхПоследнейСтрокиСотрудника.Сотрудник = Сотрудник;
			ОписаниеДанныхПоследнейСтрокиСотрудника.ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
			ЗарплатаКадрыРасширенныйКлиентСервер.ЗаполнитьСтрокуМногосотрудниковогоДокумента(ОписаниеДанныхПоследнейСтрокиСотрудника, НоваяСтрокаСотрудников, Объект);
			
		Иначе
			НоваяСтрокаСотрудников.Сотрудник = Сотрудник;
		КонецЕсли; 
		
		УстановитьРасшифровкуСтроки(НоваяСтрокаСотрудников.ПолучитьИдентификатор());
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЮТР_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	КадровыйУчетФормы.ФормаКадровогоДокументаПриСозданииНаСервере(ЭтаФорма);
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ПриПолученииДанныхНаСервере(Объект);
		ЮТР_ЗаполнитьДанныеФормыПоОрганизации();
		
	КонецЕсли; 
	
	ЗарплатаКадрыРасширенный.УстановитьВидимостьКомандПечатиМногофункциональногоДокумента(ЭтаФорма);
	
	ЗарплатаКадрыРасширенный.УстановитьПредупреждающуюНадписьВМногофункциональныхДокументах(ЭтаФорма, "НачисленияУтверждены");
	
	Если ИспользуетсяРасчетЗарплаты И Не ОграниченияНаУровнеЗаписей.ИзменениеБезОграничений И Объект.НачисленияУтверждены Тогда 
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	КадровыйУчетФормыРасширенный.РазместитьКомандуПроверкиШтатномуРасписанию(ЭтаФорма);
	
	УстановитьВыполнениеОбработчиковСобытия(Ложь);

КонецПроцедуры

&НаСервере
Процедура ЮТР_ЗаполнитьДанныеФормыПоОрганизации()
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли; 
	
	ЗапрашиваемыеЗначения = Новый Структура;
	ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.Организация");
	
	ЗапрашиваемыеЗначения.Вставить("Руководитель", "Объект.Руководитель");
	ЗапрашиваемыеЗначения.Вставить("ДолжностьРуководителя", "Объект.ДолжностьРуководителя");
	
	// ЮТ начало изменения 
	// {32 542}, 26.03.2015
	// заполнение дополнительных полей на закладке  Трудовой Договор.
	ЗапрашиваемыеЗначения.Вставить("Руководитель", "Объект.ЮТР_РуководительТД");
	ЗапрашиваемыеЗначения.Вставить("ДолжностьРуководителя", "Объект.ЮТР_ДолжностьРуководителяТД");
	//ЮТР окончание изменения
	
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗапрашиваемыеЗначения, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Организация"));	
	
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ОрганизацияПриИзменении(Элемент)
	
	ЮТР_ЗаполнитьДанныеФормыПоОрганизации();
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	УстановитьВыполнениеОбработчиковСобытия(Ложь);

КонецПроцедуры

//ЮТР окончание изменения
