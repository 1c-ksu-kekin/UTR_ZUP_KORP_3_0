
//ЮТР начало изменения 
//{32 563}, 30.04.2015
//На форму добавлена возможность вносить статью КД с сохранением в регистр сведений Статьи КД

&НаКлиенте
Процедура ЮТР_СтатьиКДПериодСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(
		ЭтаФорма,
		"ЮТР_СтатьиКДПериод",
		"ЮТР_СтатьиКДПериодСтрокой",
		Модифицированность);

	ЮТР_СтатьиКД.Период = ЮТР_СтатьиКДПериод;

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_СтатьиКДПериодСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ЮТР_СтатьиКДПериодСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(
		ЭтаФорма,
		ЭтаФорма,
		"ЮТР_СтатьиКДПериод",
		"ЮТР_СтатьиКДПериодСтрокой", ,
		Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_СтатьиКДПериодСтрокойНачалоВыбораЗавершение(ЗначениеВыбрано, ДополнительныеПараметры) Экспорт

	ЮТР_СтатьиКД.Период = ЮТР_СтатьиКДПериод;
	
КонецПроцедуры	

&НаКлиенте
Процедура ЮТР_СтатьиКДПериодСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(
		ЭтаФорма,
		"ЮТР_СтатьиКДПериод",
		"ЮТР_СтатьиКДПериодСтрокой", 
		Направление,
		Модифицированность);

	ЮТР_СтатьиКД.Период = ЮТР_СтатьиКДПериод;

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_СтатьиКДПериодСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_СтатьиКДПериодСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	ЮТР_СтатьиКД.Период = ЮТР_СтатьиКДПериод;

КонецПроцедуры

&НаСервере
Процедура ЮТР_ПроинициализироватьФорму()
	
	Если ЮТР_СтатьиКДПрежняя = Неопределено Тогда
		РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(
			ЭтаФорма,
			"ЮТР_СтатьиКД",
			Объект.Ссылка); 
	КонецЕсли;
	
	ЮТР_ОбновитьПолеСтатьяКДПериод(ЭтаФорма, ТекущаяДатаСеанса());
		
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаборЗаписейПериодическихСведений(ИмяРегистра, ВедущийОбъект) Экспорт
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписей(ЭтаФорма, ИмяРегистра, ВедущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
		
	СотрудникиКлиент.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
	Если ИмяСобытия = "ОтредактированаИстория" И Объект.Ссылка = Источник Тогда
		
		Если Параметр.ИмяРегистра = "ЮТР_СтатьиКД" Тогда
			Если ЮТР_СтатьиКДНаборЗаписейПрочитан Тогда
				РедактированиеПериодическихСведенийКлиент.ОбработкаОповещения(
					ЭтаФорма,
					Объект.Ссылка,
					ИмяСобытия,
					Параметр,
					Источник);
				КонецЕсли;
				ЮТР_ОбновитьПолеСтатьяКДПериод(ЭтаФорма, ОбщегоНазначенияКлиент.ДатаСеанса());
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЮТР_ОбновитьПолеСтатьяКДПериод(Форма, ДатаСеанса)
	
	Если НЕ ЗначениеЗаполнено(Форма.ЮТР_СтатьиКД.СтатьяКД) И НЕ ЗначениеЗаполнено(Форма.ЮТР_СтатьиКД.Период) Тогда
		Форма.ЮТР_СтатьиКД.Период = '00010101';			
	ИначеЕсли ЗначениеЗаполнено(Форма.ЮТР_СтатьиКД.СтатьяКД) И НЕ ЗначениеЗаполнено(Форма.ЮТР_СтатьиКД.Период) Тогда
		Форма.ЮТР_СтатьиКД.Период = НачалоМесяца(ДатаСеанса);
	КонецЕсли;
	
	Форма.ЮТР_СтатьиКДПериод = Форма.ЮТР_СтатьиКД.Период;
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(Форма, "ЮТР_СтатьиКДПериод", "ЮТР_СтатьиКДПериодСтрокой");
	
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_СтатьиКДСтатьяКДПриИзменении(Элемент)
	
	ЮТР_ОбновитьПолеСтатьяКДПериод(ЭтаФорма, ОбщегоНазначенияКлиент.ДатаСеанса());
	
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ЗапроситьРежимИзмененияСтатьиКД(ДатаИзменения, Отказ)
	
	Оповещение = Новый ОписаниеОповещения("ЮТР_ЗапроситьРежимИзмененияСтатьиКДЗавершение", ЭтотОбъект);
	
	ТекстКнопкиДа = НСтр("ru = 'Изменилась статья КД'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru =  'При редактировании изменилась статья КД. 
						|Если просто исправлены прежние данные (они были ошибочны), нажмите ""Исправлена ошибка"".
						|Если изменилась статья КД %1, нажмите ""Изменилась статья КД""'"), 
			Формат(ДатаИзменения, "ДФ='д ММММ гггг ""г""'"));
			
	РедактированиеПериодическихСведенийКлиент.ЗапроситьРежимИзмененияРегистра(ЭтаФорма, "ЮТР_СтатьиКД", ТекстВопроса, ТекстКнопкиДа, Отказ, Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ЗапроситьРежимИзмененияСтатьиКДЗавершение(Отказ, ОповещениеЗавершения) Экспорт
	
	Если Не Отказ Тогда
		
		Если ПроверитьЗаполнение() Тогда
			
			ВозвращаемыйПараметр = Новый Структура;
			ВозвращаемыйПараметр.Вставить("ИмяФормы", ИмяФормы);
			ВозвращаемыйПараметр.Вставить("АдресВХранилище", ЮТР_АдресДанныхДополнительнойФормыНаСервере(СотрудникиКлиентСервер.ОписаниеДополнительнойФормы(ИмяФормы)));
			
			Оповестить(
			"ИзмененыДанныеДополнительнойФормы",
			ВозвращаемыйПараметр,
			ЭтаФорма);
			
			Если ЗакрытьПослеЗаписи Тогда
				ЮТР_СтатьиКДНоваяЗапись = Истина;	
			КонецЕсли;
			
			ЮТР_ЗаписатьСтатьиКДВРегистр();
			
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли; 
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ЮТР_АдресДанныхДополнительнойФормыНаСервере(ОписаниеДополнительнойФормы)
	
	Возврат СотрудникиФормы.АдресДанныхДополнительнойФормы(ОписаниеДополнительнойФормы, ЭтаФорма);
	
КонецФункции

&НаСервере
Процедура ЮТР_ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	РедактированиеПериодическихСведений.ПроверитьЗаписьВФорме(ЭтаФорма, "ЮТР_СтатьиКД", Объект.Ссылка, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеИзХранилищаВФормуНаСервере(Параметр) Экспорт
	
	СотрудникиФормы.ПрочитатьДанныеИзХранилищаВФорму(
		ЭтаФорма,
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы(ИмяФормы),
		ЮТР_АдресДанныхДополнительнойФормыНаСервере(СотрудникиКлиентСервер.ОписаниеДополнительнойФормы(ИмяФормы)));
	
КонецПроцедуры

&НаСервере
Процедура ЮТР_ЗаписатьСтатьиКДВРегистр()
	
	РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(ЭтаФорма, "ЮТР_СтатьиКД", Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ЮТР_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВыполнениеПослеОбработчиковСобытия("ЮТР_ПриСозданииНаСервереПосле", Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ЮТР_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка, ДополнительныйПараметр)
	
	//ЮТР начало изменения 
	//{32 563}, 30.04.2015
	//На форму добавлена возможность вносить статью КД с сохранением в регистр сведений Статьи КД
	СотрудникиФормы.ПрочитатьДанныеИзХранилищаВФорму(
		ЭтаФорма,
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы(ИмяФормы),
		ЮТР_АдресДанныхДополнительнойФормыНаСервере(СотрудникиКлиентСервер.ОписаниеДополнительнойФормы(ИмяФормы)));
	ЮТР_ПроинициализироватьФорму();
	//ЮТР окончание изменения

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	УстановитьВыполнениеПослеОбработчиковСобытия("ЮТР_ПередЗаписьюПосле", Неопределено);
	
КонецПроцедуры

&НаКлиенте 
Процедура ЮТР_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи, ДополнительныйПараметр)
	
	//ЮТР начало изменения 
	//{32 563}, 30.04.2015
	//На форму добавлена возможность вносить статью КД с сохранением в регистр сведений Статьи КД
	ЮТР_ЗапроситьРежимИзмененияСтатьиКД(ЮТР_СтатьиКДПериод, Ложь);
	//ЮТР окончание изменения

КонецПроцедуры
//ЮТР окончание изменения
