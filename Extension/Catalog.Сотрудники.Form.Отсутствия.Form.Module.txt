
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ЮТР_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВыполнениеПослеОбработчиковСобытия("ЮТР_ПриСозданииНаСервереПосле", Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ЮТР_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка, ДополнительныеПараметры)
	
	//ЮТР начало изменения
	//{32537}, 13.05.2015
	// Перезаполняет текст и параметры запроса динамического списка "Отсутствия"
	ЮТР_ПерезаполнитьДинамическийСписок();
	//ЮТР окончание изменения
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик собития при изменении динамического списка "Отсутствия"
// Выполняет повтрное формирования текста и параметров запроса подставляемого в динамический список "Отсутствия"
&НаКлиенте
Процедура ЮТР_ОтсутствияПричинаОтсутствияПриИзменении(Элемент)
	
	ЮТР_ПерезаполнитьДинамическийСписок();	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет принудительное заполнение текста и параметры запроса, подставляемого в динамический список "Отсутствия"
&НаСервере
Процедура ЮТР_ПерезаполнитьДинамическийСписок()
	
	НовыеПараметрыЗапроса = ЮТР_СборкаЗапросаПоОтсутствиям();
	ЭтотОбъект.Отсутствия.ТекстЗапроса = НовыеПараметрыЗапроса.ТекстЗапроса;
	ИндексПараметра = 1;
	
	Для Каждого ТекПараметр из НовыеПараметрыЗапроса.Параметры Цикл 
		
		ЭтотОбъект.Отсутствия.Параметры.УстановитьЗначениеПараметра("ПараметрПолеСсылки"+Строка(ИндексПараметра),ТекПараметр.СсылкаНаДок);
		ИндексПараметра = ИндексПараметра + 1;
		
	КонецЦикла;
	
КонецПроцедуры	

// Определяет текст и параметры запроса, подставляемого в динамический список "Отсутствия"
//
// Возвращаемое значение:
// Структура содержащая текст запроса и таблица значений параметров, подставляемых в запрос.
&НаСервере
Функция ЮТР_СборкаЗапросаПоОтсутствиям()
	
	ТаблицаДопОтпусков = Новый ТаблицаЗначений;
	ТаблицаДопОтпусков.Колонки.Добавить("СсылкаНаДок");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтпускДополнительныеОтпуска.Ссылка КАК Ссылка,
		|	ОтпускДополнительныеОтпуска.ВидОтпуска КАК ВидОтпуска,
		|	ОтпускДополнительныеОтпуска.Ссылка.ПредоставитьОсновнойОтпуск,
		|	ОтпускДополнительныеОтпуска.Ссылка.ПредоставитьДополнительныйОтпуск,
		|	ОтпускДополнительныеОтпуска.КоличествоДней
		|ИЗ
		|	Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
		|ГДЕ
		|	ОтпускДополнительныеОтпуска.Ссылка.Сотрудник = &Сотрудник
		|ИТОГИ
		|	МАКСИМУМ(ВидОтпуска)
		|ПО
		|	Ссылка";
	
	Запрос.УстановитьПараметр("Сотрудник", СотрудникСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ДополнениеЗапроса = "";
	
	Если ВыборкаСсылка.Количество()>0 ТОгда
		ЕстьДопОтпуска = Истина;
	Иначе
		ЕстьДопОтпуска = Ложь;
	КонецЕсли;	

	Если ЕстьДопОтпуска Тогда
		
	ДополнениеЗапроса = "ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ";
	Индекс = 0;

		Пока ВыборкаСсылка.Следующий() Цикл
			ТекСтр = ТаблицаДопОтпусков.Добавить();
			Индекс = Индекс+1;
						ПолеВидотпуска = "";
						Если ВыборкаСсылка.ПредоставитьОсновнойОтпуск = Ложь ТОгда 
							ПолеВидотпуска = "";
						ИначеЕсли ВыборкаСсылка.ПредоставитьОсновнойОтпуск = Истина ТОгда 	
							ПолеВидотпуска = "Отпуск основной";
						КонецЕсли;							
							
			ВыборкаДетали = ВыборкаСсылка.Выбрать();
			Пока ВыборкаДетали.Следующий() Цикл
				Если ЗначениеЗаполнено(ПолеВидотпуска) ТОгда
					ПолеВидотпуска = ПолеВидотпуска + ", ";
				КонецЕсли;
					ПолеВидОтпуска = ПолеВидотпуска + Строка(ВыборкаДетали.ВидОтпуска);
			КонецЦикла;
			
			Если Индекс > 1 Тогда		 
				ДополнениеЗапроса = ДополнениеЗапроса +
				"
				|
				| ОБЪЕДИНИТЬ
				|
				| ВЫБРАТЬ
				|";		 
			КонецЕсли;	 
			
			ДополнениеЗапроса = ДополнениеЗапроса +
			"
			| &ПараметрПолеСсылки"+Строка(Индекс)+" КАК Ссылка,
			| "+""""+ ПолеВидОтпуска+""" "+ " КАК ВидОтпуска";		
			ТекСтр.СсылкаНаДок = ВыборкаСсылка.Ссылка;		 
			
		КонецЦикла;
	
		ДополнениеЗапроса = ДополнениеЗапроса + 
		"
		|) КАК ДопИнфаОтпусков
		|ПО ЖурналДокументовОтсутствия.Ссылка = ДопИнфаОтпусков.Ссылка
		|";	
	КонецЕсли;	

	ОсновнойТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЖурналДокументовОтсутствия.Ссылка,
	|	ЖурналДокументовОтсутствия.Дата,
	|	ЖурналДокументовОтсутствия.ПометкаУдаления,
	|	ЖурналДокументовОтсутствия.Номер,
	|	ЖурналДокументовОтсутствия.Проведен,
	|	ЖурналДокументовОтсутствия.Организация,
	|	ЖурналДокументовОтсутствия.Сотрудник,
	|	ВЫБОР
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.Отпуск
	|					И НЕ ЖурналДокументовОтсутствия.Ссылка.ПредоставитьОсновнойОтпуск
	|					И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьДополнительныйОтпуск
	|				ИЛИ ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.Отпуск
	|					И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьОсновнойОтпуск
	|					И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьДополнительныйОтпуск
	|			ТОГДА ДатыДопОтпусков.ДатаНачала
	|		ИНАЧЕ ЖурналДокументовОтсутствия.ДатаНачала
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.Отпуск
	|					И НЕ ЖурналДокументовОтсутствия.Ссылка.ПредоставитьОсновнойОтпуск
	|					И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьДополнительныйОтпуск
	|				ИЛИ ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.Отпуск
	|					И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьОсновнойОтпуск
	|					И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьДополнительныйОтпуск
	|			ТОГДА ДатыДопОтпусков.ДатаОкончания
	|		ИНАЧЕ ЖурналДокументовОтсутствия.ДатаОкончания
	|	КОНЕЦ КАК ДатаОкончания,
	|	ЖурналДокументовОтсутствия.Рассчитан,
	|	ЖурналДокументовОтсутствия.Ответственный,
	|	ЖурналДокументовОтсутствия.Комментарий,
	|	ЖурналДокументовОтсутствия.Тип,
	|	ВЫБОР
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.ОплатаДнейУходаЗаДетьмиИнвалидами
	|			ТОГДА ТаблицаДниУходаЗаДетьмиИнвалидами.Количество
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.Отпуск
	|						И НЕ ЖурналДокументовОтсутствия.Ссылка.ПредоставитьОсновнойОтпуск
	|						И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьДополнительныйОтпуск
	|					ТОГДА ТаблицаДополнительныеОтпуска.КоличествоДней
	|				КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.Отпуск
	|						И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьОсновнойОтпуск
	|						И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьДополнительныйОтпуск
	|					ТОГДА ЖурналДокументовОтсутствия.Ссылка.КоличествоДнейОсновногоОтпуска + ТаблицаДополнительныеОтпуска.КоличествоДней
	|				ИНАЧЕ РАЗНОСТЬДАТ(ЖурналДокументовОтсутствия.ДатаНачала, ЖурналДокументовОтсутствия.ДатаОкончания, ДЕНЬ) + 1
	|			КОНЕЦ
	|	КОНЕЦ КАК ДлительностьОтсутствия,
	|	ВЫБОР
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.БольничныйЛист
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ЖурналДокументовОтсутствия.Ссылка.ПричинаНетрудоспособности)
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.Командировка
	|			ТОГДА ВЫРАЗИТЬ(ЖурналДокументовОтсутствия.Ссылка.Цель КАК СТРОКА(100))
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.ОплатаДнейУходаЗаДетьмиИнвалидами
	|			ТОГДА ""Уход за детьми инвалидами""
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.ОплатаПоСреднемуЗаработку
	|			ТОГДА ЖурналДокументовОтсутствия.Ссылка.ВидВремени
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.Отгул
	|			ТОГДА ""Отгул""
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.ОтпускПоУходуЗаРебенком
	|			ТОГДА ""Отпуск по уходу за ребенком""
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.ВозвратИзОтпускаПоУходуЗаРебенком
	|			ТОГДА ""Возврат из отпуска по уходу за ребенком""
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.Отпуск
	|			ТОГДА ВЫБОР
	|	    				КОГДА ЖурналДокументовОтсутствия.Ссылка.ПредоставитьОсновнойОтпуск
	|							И НЕ ЖурналДокументовОтсутствия.Ссылка.ПредоставитьДополнительныйОтпуск
	|						  ТОГДА ""Отпуск основной""
	|"+ ?(ЕстьДопОтпуска,"  КОГДА НЕ ЖурналДокументовОтсутствия.Ссылка.ПредоставитьОсновнойОтпуск
	|							И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьДополнительныйОтпуск
	|						  ТОГДА ДопИнфаОтпусков.ВидОтпуска
	|					    КОГДА ЖурналДокументовОтсутствия.Ссылка.ПредоставитьОсновнойОтпуск
	|							И ЖурналДокументовОтсутствия.Ссылка.ПредоставитьДополнительныйОтпуск
	|						  ТОГДА ДопИнфаОтпусков.ВидОтпуска","") + "
	|				КОНЕЦ
	|		КОГДА ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.ОтпускБезСохраненияОплаты
	|			ТОГДА ЖурналДокументовОтсутствия.Ссылка.ВидОтпуска.Наименование
	|		ИНАЧЕ ЖурналДокументовОтсутствия.Ссылка.ВидОтсутствия.Наименование
	|	КОНЕЦ КАК ПричинаОтсутствия
	|	
	|ИЗ
	|	ЖурналДокументов.Отсутствия КАК ЖурналДокументовОтсутствия
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОплатаДнейУходаЗаДетьмиИнвалидамиДниУхода.Ссылка КАК СсылкаНаДок,
	|			КОЛИЧЕСТВО(1) КАК Количество
	|		ИЗ
	|			Документ.ОплатаДнейУходаЗаДетьмиИнвалидами.ДниУхода КАК ОплатаДнейУходаЗаДетьмиИнвалидамиДниУхода
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ОплатаДнейУходаЗаДетьмиИнвалидамиДниУхода.Ссылка) КАК ТаблицаДниУходаЗаДетьмиИнвалидами
	|		ПО ЖурналДокументовОтсутствия.Ссылка = ТаблицаДниУходаЗаДетьмиИнвалидами.СсылкаНаДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОтпускДополнительныеОтпуска.Ссылка КАК Ссылка,
	|			СУММА(ОтпускДополнительныеОтпуска.КоличествоДней) КАК КоличествоДней,
	|			МИНИМУМ(ОтпускДополнительныеОтпуска.ДатаНачала) КАК ДатаНачала,
	|			МАКСИМУМ(ОтпускДополнительныеОтпуска.ДатаОкончания) КАК ДатаОкончания
	|		ИЗ
	|			Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ОтпускДополнительныеОтпуска.Ссылка) КАК ТаблицаДополнительныеОтпуска
	|		ПО ЖурналДокументовОтсутствия.Ссылка = ТаблицаДополнительныеОтпуска.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ(
	|						ВЫБРАТЬ
	|								ВложенныйЗапрос.Ссылка КАК Ссылка,
	|								МИНИМУМ(ВложенныйЗапрос.ДатаНачала) КАК ДатаНачала,
	|								МАКСИМУМ(ВложенныйЗапрос.ДатаОкончания) КАК ДатаОкончания
	|						ИЗ
	|						(ВЫБРАТЬ
	|								ОтпускДополнительныеОтпуска.Ссылка КАК Ссылка,
	|								ОтпускДополнительныеОтпуска.ДатаНачала КАК ДатаНачала,
	|								ОтпускДополнительныеОтпуска.ДатаОкончания КАК ДатаОкончания
	|							ИЗ
	|								Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
	|
	|						ОБЪЕДИНИТЬ				
	|				
	|						ВЫБРАТЬ
	|								ОтпускДополнительныеОтпуска.Ссылка,
	|								ОтпускДополнительныеОтпуска.Ссылка.ДатаНачалаОсновногоОтпуска,
	|								ОтпускДополнительныеОтпуска.Ссылка.ДатаОкончанияОсновногоОтпуска
	|							ИЗ
	|								Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
	|			
	|						СГРУППИРОВАТЬ ПО
	|								ОтпускДополнительныеОтпуска.Ссылка) КАК  ВложенныйЗапрос	
	|		СГРУППИРОВАТЬ ПО
	|				ВложенныйЗапрос.Ссылка) КАК ДатыДопОтпусков
	|		ПО ЖурналДокументовОтсутствия.Ссылка = ДатыДопОтпусков.Ссылка
	|" + ?(ЕстьДопОтпуска, ДополнениеЗапроса,"") + "
	|
	|ГДЕ
	|	НЕ ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.КомандировкиСотрудников
	|	И НЕ ЖурналДокументовОтсутствия.Ссылка ССЫЛКА Документ.ОтпускаСотрудников";
	
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ТекстЗапроса",ОсновнойТекстЗапроса);
	ПараметрыЗапроса.Вставить("Параметры",ТаблицаДопОтпусков);
	
	Возврат ПараметрыЗапроса;
КонецФункции

#КонецОбласти