
&НаКлиенте
Процедура ЮТР_ПозицииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если ЮТР_УправлениеРаботойТиповыхФормВызовСервера.ОпределитьДоступностьРоли("ЮТР_ДобавлениеИзменениеШтатногоРасписания,ЮТР_ДобавлениеИзменениеШтатногоРасписанияИзменениеТерритории") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Недостаточно прав на выполнение данной операции.'"));
		Возврат;
	КонецЕсли;	

	Если Копирование Тогда
		ПредыдущаяСтрока = Элемент.ТекущиеДанные
	КонецЕсли; 
	
	НоваяСтрокаПозиции = Объект.Позиции.Добавить();
	ИдентификаторТекущейПозиции = НоваяСтрокаПозиции.ПолучитьИдентификатор();
	Элемент.ТекущаяСтрока = ИдентификаторТекущейПозиции;
	
	Если Копирование Тогда
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПозиции, ПредыдущаяСтрока);
		ТекущаяДолжность = Элемент.ТекущиеДанные.Должность;
	КонецЕсли; 
	
	УправлениеШтатнымРасписаниемКлиент.ПозицииПриНачалеРедактирования(ЭтаФорма, Истина, Копирование);
	Если Копирование Тогда
		СкопироватьДанныеПозиции(ИдентификаторТекущейПозиции, ПредыдущаяСтрока.ПолучитьИдентификатор());
	КонецЕсли;
	
	УправлениеШтатнымРасписаниемКлиент.ГрафикРаботыСотрудников(ЭтаФорма, Объект.Организация, Элемент.ТекущиеДанные.Подразделение);
		
	ЮТР_ОткрытьДанныеПозиции(Элемент.ТекущаяСтрока);
	
	УстановитьВыполнениеОбработчиковСобытия(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураОтбор = Новый Структура;
	СтруктураОтбор.Вставить("ВидДокумента", "ИзменениеШтатногоРасписания");
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Отбор", СтруктураОтбор);
	ОткрытьФорму("Справочник.ЮТР_ВидыДокументов.ФормаВыбора", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ЮТР_ОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Процедура ЮТР_ОбработкаВыбораНаСервере(ВыбранноеЗначение)
	
	Объект.ЮТР_ВидДокумента = ВыбранноеЗначение;
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ЮТР_УправлениеРаботойТиповыхФормВызовСервера.УстановитьСвойстваЭлементовУправленияПоВидуДокумента(ВыбранноеЗначение, ЭтаФорма, "Документ.ИзменениеШтатногоРасписания");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ПозицииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ЮТР_ОткрытьДанныеПозиции(Элемент.ТекущаяСтрока);
	УстановитьВыполнениеОбработчиковСобытия(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ОткрытьДанныеПозиции(ИдентификаторСтрокиТекущейПозиции)
	
	ДанныеПозиции = ОписаниеДанныхПозицииВХранилище(ИдентификаторСтрокиТекущейПозиции);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", ДанныеПозиции.Ключ);
	ПараметрыОткрытия.Вставить("АдресДанныхПозицииВХранилище", ДанныеПозиции.АдресДанныхПозицииВХранилище);
	ПараметрыОткрытия.Вставить("ЮТР_ВидДокумента", Объект.ЮТР_ВидДокумента);
	
	ОткрытьФорму("Справочник.ШтатноеРасписание.ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЮТР_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.Ключ) Тогда
		Если ЮТР_УправлениеРаботойТиповыхФормВызовСервера.ОпределитьДоступностьРоли("ЮТР_ДобавлениеИзменениеШтатногоРасписания") Тогда
			Объект.ЮТР_ВидДокумента = ЮТР_УправлениеРаботойТиповыхФормВызовСервера.НайтиВидОперацииПоРоли("ЮТР_ДобавлениеИзменениеШтатногоРасписанияИзменениеПраваНаДосрочнуюПенсию");
		Иначе
			Объект.ЮТР_ВидДокумента = ЮТР_УправлениеРаботойТиповыхФормВызовСервера.НайтиВидОперацииПоРоли("ЮТР_ДобавлениеИзменениеНачисленийШтатногоРасписания");
		КонецЕсли;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Объект.ЮТР_ВидДокумента) Тогда
		ЮТР_УправлениеРаботойТиповыхФормВызовСервера.УстановитьСвойстваЭлементовУправленияПоВидуДокумента(Объект.ЮТР_ВидДокумента, ЭтаФорма, "Документ.ИзменениеШтатногоРасписания");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЮТР_ВидДокументаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры







